QActor serviceaccessgui context ctxcoldstorageservice {
	
	State s0 initial {
		printCurrentMessage
		println("$name START ") color blue
	}
	Goto sendrequest
	
	// send the first request to the service
	State sendrequest {
		[# var Load = 50  #]
		println("$name SEND REQUEST: $Load kg") color blue
		request coldstorageservice -m storerequest : storerequest($Load)
	}
	Transition t0 whenReply storeaccepted -> sendticket
				whenReply storerefused -> endwork
				
	// send the second request to the service	
	State sendticket {
		onMsg( storeaccepted : storeaccepted( TICKET ) ) {
			[# var Ticket = "${payloadArg(0)}" #]
			println("$name MOVING TO INDOOR --> ticket: $Ticket") color blue
			delay 3000 // time for the user to move to the indoor port
			
			println("$name SENDING TICKET: $Ticket") color blue
			request coldstorageservice -m ticketrequest : ticketrequest($Ticket)
		}
	}
	Transition t0 whenReply chargetaken -> endwork
				whenReply chargerefused -> endwork
				
	// end work
	State endwork {
		onMsg( storerefused : storerefused( KG ) ) {
			println("$name store refused. END WORK") color blue
		}
		onMsg( chargetaken : chargetaken( KG ) ) {
			println("$name charge taken. END WORK") color blue
		}
		onMsg( chargerefused : chargerefused( TIME ) ) {
			println("$name charge refused. END WORK") color blue
		}
	}
}